plugins {
    id 'java'
}

group 'com.guflimc.brick.placeholders'
version '1.0-SNAPSHOT'

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.17
    targetCompatibility = 1.17

    group = this.group;
    version = this.version;

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
        maven { url 'https://repo.spongepowered.org/maven' }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:23.0.0'
        compileOnly 'net.kyori:adventure-api:4.10.1'
    }
}

configure(subprojects.findAll { it.name.contains('-api') }) {
    apply plugin: 'maven-publish'

    publishing {
        repositories {
            maven {
                name = "Jorisg"
                url = uri("https://repo.jorisg.com/snapshots")
                credentials(org.gradle.api.credentials.PasswordCredentials)
            }
        }

        publications {
            release(MavenPublication) {
                from components.java
                groupId = rootProject.group
                artifactId = project.name
                version = rootProject.version
            }
        }
    }

//    javadoc {
//        source project.sourceSets.main.compileClasspath //configurations.runtimeClasspath.collect { zipTree(it) }
//        include '**/*.java'
//        options.addStringOption('Xdoclint:none', '-quiet')
//    }
}

def javadocProjects = [
        "api",
        "minestom-api",
        "spigot-api",
]

javadoc {
    source javadocProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(javadocProjects.collect { project(it).sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/docs/javadoc")
}
